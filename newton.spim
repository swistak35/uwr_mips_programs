# Calculating newton symbol (binomial coefficient)
# Input: n (integer > 0), k (integer, 0 < k < n)
# Output: n choose k

	.globl main
main:	li $v0, 5
	syscall
	move $a0, $v0
	li $v0, 5
	syscall
	move $a1, $v0
	jal BICO
	move $a0, $v0
	li $v0, 1
	syscall
	li $v0, 10
	syscall

	.globl BICO
BICO:	addi $sp, $sp, -20
	sw $s1, 16($sp)
	sw $s0, 12($sp)
	sw $ra, 8($sp)
	sw $a1, 4($sp)
	sw $a0, 0($sp)
	beq $zero, $a1, BICO_1		# if a1 == 0, bico(a0, a1) = 1
	bne $a0, $a1, BICO_C		  # if a1 == a0, bico(a0, a1) = 1
BICO_1:	li $v0, 1
	addi $sp, $sp, 20
	jr $ra
BICO_C:	addi $a0, $a0, -1
	jal BICO			# bico(n-1, k)
	move $s0, $v0
	addi $a1, $a1, -1
	jal BICO			# bico(n-1, k-1)
	move $s1, $v0
	add $v0, $s0, $s1
	lw $a0, 0($sp)
	lw $a1, 4($sp)
	lw $ra, 8($sp)
	lw $s0, 12($sp)
	lw $s1, 16($sp)
	addi $sp, $sp, 20
	jr $ra
